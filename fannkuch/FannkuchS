REM based on the Go version of the Fannkuch benchmark
REM https://github.com/golang/go/blob/master/test/bench/go1/fannkuch_test.go

T% = TIME
F% = FNFannkuch%(8)
PRINT TIME - T%
PRINT F%

DEF FNFannkuch%(n%)
  IF n% < 1 THEN
    <-0
  ENDIF

  LOCAL n1%
  LOCAL i%
  LOCAL j%
  LOCAL r%
  LOCAL didpr%
  LOCAL flipsMax%
  LOCAL flips%
  LOCAL k%
  LOCAL tmp%
  LOCAL quitl%
  n1% = n% - 1

  LOCAL DIM perm%(n1%)
  LOCAL DIM perm1%(n1%)
  LOCAL DIM cnt%(n1%)

  FOR i% = 0 TO n1%
    perm1%(i%) = i%
  NEXT

  r% = n%
  REPEAT
    IF didpr% < 30 THEN
      didpr% += 1
    ENDIF

    WHILE r% <> 1
      cnt%(r%-1) = r%
      r% -= 1
    ENDWHILE

    IF perm1%(0) <> 0 AND perm1%(n1%) <> n1% THEN
      flips% = 0
      i% = 1
      WHILE i% < n%
        perm%(i%) = perm1%(i%)
        i% += 1
      ENDWHILE
      k% = perm1%(0)
      REPEAT
        i% = 1
        j% = k% - 1
        WHILE i% < j%
          tmp% = perm%(i%)
          perm%(i%) = perm%(j%)
          perm%(j%) = tmp%
          i% += 1
          j% -= 1
        ENDWHILE
        flips% += 1
        j% = perm%(k%)
        perm%(k%) = k%
        k% = j%
      UNTIL k% = 0
      IF flipsMax% < flips% THEN
        flipsMax% = flips%
      ENDIF
    ENDIF

    quitl% = FALSE
    WHILE (r% < n%) AND (NOT quitl%)
      tmp% = perm1%(0)
      i% = 0
      WHILE i% < r%
        perm1%(i%) = perm1%(i%+1)
        i% += 1
      ENDWHILE
      perm1%(r%) = tmp%
      cnt%(r%) -= 1
      IF cnt%(r%) > 0 THEN
        quitl% = TRUE
      ELSE
        r% +=1
      ENDIF
    ENDWHILE

    IF r% = n% THEN
      <-flipsMax%
    ENDIF
  UNTIL FALSE
<-0